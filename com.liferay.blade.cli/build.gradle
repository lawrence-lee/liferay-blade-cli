import de.undercouch.gradle.tasks.download.Download

buildscript {
	dependencies {
		classpath group: "de.undercouch", name: "gradle-download-task", version: "3.3.0"
	}

	repositories {
		mavenCentral()
	}
}

dependencies {
	testCompile gradleTestKit()
}

task createDepsZip(type:Zip) {
	from "${projectDir}/../com.liferay.blade.gradle/generated/"
	baseName = "deps"
	destinationDir = new File("${projectDir}")
	include "*.jar"
}

task createWrapperZip(type:Zip) {
	from "${projectDir}/../"
	baseName = "wrapper"
	destinationDir = new File("${projectDir}")
	include "gradle/**"
	include "gradlew*"
}

clean.doFirst {
	delete "${projectDir}/deps.zip", "${projectDir}/wrapper.zip"
}

jar {
	from createDepsZip, createWrapperZip
}

task downloadPortal(type: Download) {
	src 'https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.3-ga4/liferay-ce-portal-tomcat-7.0-ga4-20170613175008905.zip'
	dest new File("${System.getProperty('user.home')}/.liferay/bundles", 'liferay-ce-portal-tomcat-7.0-ga4-20170613175008905.zip')
	onlyIfNewer true
}

task unzipPortal(dependsOn: downloadPortal, type: Copy) {
	from zipTree(downloadPortal.dest)
	into buildDir
}

test {
	dependsOn createDepsZip, createWrapperZip, unzipPortal
	
	systemProperty 'buildDir', "${buildDir}"
	systemProperty 'liferay.home', "${buildDir}/liferay-ce-portal-7.0-ga4"
}